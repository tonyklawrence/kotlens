group 'com.tonylawrence'

buildscript {
  ext.kotlens_version = '0.1.1'
  ext.kotlin_version = '1.0.+'

  repositories { mavenCentral() }
  dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

apply plugin: 'kotlin'

repositories { jcenter() }
repositories { mavenCentral() }

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testCompile "junit:junit:4.+"
  testCompile "org.hamcrest:hamcrest-core:1.+"
  testCompile "com.natpryce:hamkrest:1.2.+"
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives sourcesJar
}

jar {
  manifest {
    attributes 'Implementation-Title': 'kotlens',
      'Implementation-Vendor': 'com.tonylawrence',
      'Implementation-Version': kotlens_version
    }
}

apply plugin: 'signing'

signing {
    required { hasProperty("sign") || gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: project.properties["ossrh.username"], password: project.properties["ossrh.password"])
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: project.properties["ossrh.username"], password: project.properties["ossrh.password"])
            }

            pom.project {
                name 'Kotlens'
                version "$kotlens_version"
                packaging 'jar'
                description 'Lens library for Kotlin'
                url 'https://github.com/tonyklawrence/kotlens'

                scm {
                    connection 'git@github.com:tonyklawrence/kotlens.git'
                    url 'https://github.com/tonyklawrence/kotlens'
                }

                licenses {
                    license { name 'Apache 2.0'; url 'http://opensource.org/licenses/Apache-2.0' }
                }

                developers {
                    developer { id 'tonyklawrence'; name 'Tony Lawrence' }
                }
            }
        }
    }
}
